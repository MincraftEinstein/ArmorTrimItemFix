plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}

neoForge {
    version = neoforge_version

    parchment {
        minecraftVersion = parchment_minecraft_version
        mappingsVersion = parchment_version
    }

    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric

            def dir = project.getLayout().getProjectDirectory().dir('runs/' + it.name)
            if (!dir.asFile.exists()) {
                dir.asFile.mkdir()
            }
            getGameDirectory().convention(dir)
        }

        client {
            client()
        }

        server {
            server()
        }

        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--server', '--client', '--dev', '--reports', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', project.project(":common").file('src/main/resources/').getAbsolutePath()
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    implementation("maven.modrinth:more-armor-trims:${neoforge_more_trims_version}")
}
